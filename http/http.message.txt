
HTTP-message     = Request | Response      ; HTTP/1.1 messages  (both are generic-messages, RFC 822)

Request          = Request-Line            ; Section 5.1
                 *(( general-header        ; Section 4.5
                  | request-header         ; Section 5.3
                  | entity-header ) CRLF)  ; Section 7.1
                 CRLF
                 [ message-body ]          ; Section 4.3
                 
Request-Line     = Method SP Request-URI SP HTTP-Version CRLF
                 
Method           = "OPTIONS"                ; Section 9.2
                 | "GET"                    ; Section 9.3
                 | "HEAD"                   ; Section 9.4
                 | "POST"                   ; Section 9.5
                 | "PUT"                    ; Section 9.6
                 | "DELETE"                 ; Section 9.7
                 | "TRACE"                  ; Section 9.8
                 | "CONNECT"                ; Section 9.9
                 | extension-method

Request-URI      = "*" | absoluteURI | abs_path | authority
                 
request-header   = Accept                   ; Section 14.1
                 | Accept-Charset           ; Section 14.2
                 | Accept-Encoding          ; Section 14.3
                 | Accept-Language          ; Section 14.4
                 | Authorization            ; Section 14.8
                 | Expect                   ; Section 14.20
                 | From                     ; Section 14.22
                 | Host                     ; Section 14.23
                 | If-Match                 ; Section 14.24
                 | If-Modified-Since        ; Section 14.25
                 | If-None-Match            ; Section 14.26
                 | If-Range                 ; Section 14.27
                 | If-Unmodified-Since      ; Section 14.28
                 | Max-Forwards             ; Section 14.31
                 | Proxy-Authorization      ; Section 14.34
                 | Range                    ; Section 14.35
                 | Referer                  ; Section 14.36
                 | TE                       ; Section 14.39
                 | User-Agent               ; Section 14.43



extension-method = token
generic-message  = start-line
                   *(message-header CRLF)
                   CRLF
                   [ message-body ]
start-line       = Request-Line | Status-Line
                 
message-header   = field-name ":" [ field-value ]
field-name       = token
field-value      = *( field-content | LWS )
field-content    = <the OCTETs making up the field-value
                   and consisting of either *TEXT or combinations
                   of token, separators, and quoted-string>
                 
message-body     = entity-body
			           | <entity-body encoded as per Transfer-Encoding
                 
general-header   = Cache-Control            ; Section 14.9
                 | Connection               ; Section 14.10
                 | Date                     ; Section 14.18
                 | Pragma                   ; Section 14.32
                 | Trailer                  ; Section 14.40
                 | Transfer-Encoding        ; Section 14.41
                 | Upgrade                  ; Section 14.42
                 | Via                      ; Section 14.45
                 | Warning                  ; Section 14.46
